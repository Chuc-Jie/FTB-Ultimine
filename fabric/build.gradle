plugins {
	id "com.github.johnrengelman.shadow" version "5.0.0"
	id "com.matthewprenger.cursegradle"
}

def ENV = System.getenv()

configurations {
	shadowCommon
}

architectury {
	platformSetupLoomIde()
	fabric()
}

dependencies {
	minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
	mappings(minecraft.officialMojangMappings())

	modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")

	modImplementation "me.shedaniel:architectury-fabric:${rootProject.architectury_version}"
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}"

	implementation("me.shedaniel.cloth:basic-math:0.5.1")

	implementation(project(path: ":common")) {
		transitive = false
	}
	developmentFabric(project(path: ":common")) {
		transitive = false
	}
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
		transitive = false
	}

	modImplementation("com.terraformersmc:modmenu:${rootProject.mod_menu_version}")

	modRuntime("curse.maven:lithium-360438:3215441")
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
	inputs.property "version", project.version
}

shadowJar {
	configurations = [project.configurations.shadowCommon]
	classifier "dev-shadow"
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier "fabric"
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_fabric
			releaseType = "release"
			addGameVersion "Fabric"
			addGameVersion "1.16.4"
			addGameVersion "1.16.5"
			mainArtifact(remapJar.archivePath)
			relations {
				requiredDependency 'architectury-fabric'
				requiredDependency 'fabric-api'
				requiredDependency 'cloth-config'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archivesBaseName + "-fabric"
			artifact(remapJar.archivePath) {
				builtBy build
				classifier null
			}
		}
	}

	repositories {
		if (ENV.LOCAL_MAVEN) {
			maven {
				// upload to local maven repo
				url = "file://${ENV.LOCAL_MAVEN}"
			}
		}
	}
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")